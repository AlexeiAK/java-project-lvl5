plugins {
	id 'org.springframework.boot' version '2.7.2'
	id 'io.spring.dependency-management' version '1.0.12.RELEASE'
    id 'com.adarshr.test-logger' version '3.0.0'

    id 'org.liquibase.gradle' version '2.1.1'

    // For Swagger. Allows generating API doc to json
    id 'org.springdoc.openapi-gradle-plugin' version '1.3.3'
    id 'com.github.johnrengelman.processes' version '0.5.0'

	id 'java'
    id 'application'
    id 'checkstyle'
    id 'jacoco'
}

group = 'hexlet.code'
version = '1-0-SNAPSHOT'
sourceCompatibility = '17'

repositories {
    mavenCentral()
}

application {
    mainClass = 'hexlet.code.AppApplication'
}

dependencies {
    compileOnly 'org.projectlombok:lombok'


    implementation (
            'org.springframework.boot:spring-boot-starter-web',
            'org.springframework.boot:spring-boot-starter-validation',
            'org.springframework.boot:spring-boot-starter-data-jpa:2.6.2',
//            'org.springframework.boot:spring-boot-starter-security',
            'org.liquibase:liquibase-core:4.8.0',
            'io.jsonwebtoken:jjwt-api:0.11.2',
            'io.jsonwebtoken:jjwt-impl:0.11.2',
            'com.querydsl:querydsl-core:5.0.0',
            'com.querydsl:querydsl-jpa:5.0.0',
            'org.springdoc:springdoc-openapi-ui:1.6.4'
    )
    annotationProcessor (
            'com.querydsl:querydsl-apt:5.0.0:jpa',
            'org.projectlombok:lombok',
            'javax.annotation:javax.annotation-api:1.3.2',
            'org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.2.Final'
    )

    runtimeOnly (
            'io.jsonwebtoken:jjwt-jackson:0.11.2',
            'org.postgresql:postgresql',
            'com.h2database:h2'
    )

    liquibaseRuntime (
            sourceSets.main.output,
            'org.liquibase:liquibase-core:4.8.0',
            'org.liquibase.ext:liquibase-hibernate5:4.6.2',
            'org.springframework.boot:spring-boot-starter-data-jpa:2.3.0.RELEASE'
    )

    testImplementation (
            'org.springframework.boot:spring-boot-starter-test',
            'com.github.database-rider:rider-junit5:1.32.0'
    )
}

wrapper {
    gradleVersion = '7.3'
    distributionType = Wrapper.DistributionType.ALL
}


test {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

diffChangeLog {
    dependsOn compileJava
}

// Migrations
liquibase {
    activities {
        main {
            // Path of migration file
            changeLogFile 'src/main/resources/db/changelog/db.changelog-master.yaml'
            url 'jdbc:h2:./taskmgr'
            username ''
            password ''
            referenceUrl 'hibernate:spring:hexlet.code.model' +
                    '?dialect=org.hibernate.dialect.H2Dialect' +
                    '&hibernate.physical_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringPhysicalNamingStrategy'
        }
    }
}

// For Heroku deploy
task stage(dependsOn: [clean, installDist])
installDist.mustRunAfter clean